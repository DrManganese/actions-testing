plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

version = minecraft_version + "-" + mod_version
group = "io.github.drmanganese.endercrop"
archivesBaseName = "endercrop"

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"
            mods {
                endercrop {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run-server")
            property "forge.logging.console.level", "debug"
            mods {
                endercrop {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url "https://maven.k-4u.nl" }
    maven { url "https://www.cursemaven.com" }
}

dependencies {
    minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"
    implementation fg.deobf("mcjty.theoneprobe:theoneprobe:1.19.3-7.0.0-2") { transitive = false }
    implementation fg.deobf("curse.maven:jade-324717:4330163") { transitive = false }
}

jar {
    manifest {
        attributes([
                "FMLAT": "accesstransformer.cfg",
                "Specification-Title": "endercrop",
                "Specification-Vendor": "DrManganese",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": getArchiveVersion(),
                "Implementation-Vendor" :"DrManganese",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from "LICENSE"
}

if (project.hasProperty("curseforgeToken")) {
    curseforge {
        apiKey = project.findProperty("curseforgeToken")
        project {
            id = "242269"
            addGameVersion "1.19.2"
            changelogType = "markdown"
            changelog = file("CHANGELOG.md")
            releaseType = "beta"
            mainArtifact jar
        }
        options {
            debug = false
        }
    }
}

if (project.hasProperty("modrinthToken")) {
    modrinth {
        token = project.findProperty("modrinthToken")
        projectId = "3oLf6Wyp"
        versionType = "release"
        uploadFile = jar
        changelog = file("CHANGELOG.md").text
        dependencies {
            optional.project "the-one-probe"
            optional.project "jade"
        }
        debugMode = false
    }
}

task platforms (type: GradleBuild, dependsOn: jar) {
    tasks = [ 'curseforge', 'modrinth' ]
}
